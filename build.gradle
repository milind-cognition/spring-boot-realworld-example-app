plugins {
    id 'jacoco'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'java'
    id "com.netflix.dgs.codegen" version "5.6.0"
    id "com.diffplug.spotless" version "6.25.0"
    id "org.sonarqube" version "5.0.0.4638"
}

version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

spotless {
    java {
        target project.fileTree(project.rootDir) {
            include '**/*.java'
            exclude 'build/generated/**/*.*', 'build/generated-examples/**/*.*'
        }
        googleJavaFormat()
    }
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation(enforcedPlatform("com.netflix.graphql.dgs:graphql-dgs-platform-dependencies:5.5.1"))
    implementation 'com.graphql-java:graphql-java:19.2'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-hateoas'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.3.2'
    implementation 'com.netflix.graphql.dgs:graphql-dgs-spring-boot-starter:5.5.1'
    implementation 'org.flywaydb:flyway-core'
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5',
                'io.jsonwebtoken:jjwt-jackson:0.12.5'
    implementation 'joda-time:joda-time:2.12.7'
    implementation 'org.xerial:sqlite-jdbc:3.45.0.0'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation(platform('io.rest-assured:rest-assured-bom:5.4.0'))
    testImplementation 'io.rest-assured:rest-assured'
    testImplementation 'io.rest-assured:json-path'
    testImplementation 'io.rest-assured:xml-path'
    testImplementation 'io.rest-assured:spring-mock-mvc'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mybatis.spring.boot:mybatis-spring-boot-starter-test:2.3.2'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

tasks.named('clean') {
    doFirst {
        delete './dev.db'
    }
}

tasks.named('generateJava') {
    schemaPaths = ["${projectDir}/src/main/resources/schema"] // List of directories containing schema files
    packageName = 'io.spring.graphql' // The package name to use to generate sources
}

sonarqube {
    properties {
        property "sonar.coverage.exclusions", "src/main/java/io/spring/graphql/ArticleDatafetcher.java,src/main/java/io/spring/graphql/CommentDatafetcher.java,src/main/java/io/spring/graphql/exception/GraphQLCustomizeExceptionHandler.java"
    }
}

jacoco {
    toolVersion = '0.8.10'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required.set(true)
        csv.required.set(false)
        html.required.set(true)
    }
}
